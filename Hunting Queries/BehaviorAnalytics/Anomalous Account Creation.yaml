id: 0454c8d8-d0a6-42a4-8d03-f5b4fdcbd173
name: Anomalous AAD Account Creation
description: |
  'Adversaries may create a cloud account to maintain access to victim systems. With a sufficient level of access, such accounts may be used to establish secondary credentialed access that does not require persistent remote access tools to be deployed on the system. The query below generates an output of all the users performing user creation where one or more features of the activitiy deviates from the user, his peers or the tenant profile.'
requiredDataConnectors:
  - connectorId: BehaviorAnalytics
    dataTypes:
       - BehaviorAnalytics
       - AuditLogs
tactics:
  - Persistence
relevantTechniques:
  - T1136
query: |

 BehaviorAnalytics
 | where ActionType == "Add user"
 | where ActivityInsights has "True"
 | join(
 AuditLogs
 ) on $left.SourceRecordId == $right._ItemId
 | mv-expand TargetResources
 | extend Target =  iff(tostring(TargetResources.userPrincipalName) has "#EXT#",replace("_","@",tostring(split(TargetResources.userPrincipalName, "#")[0])),TargetResources.userPrincipalName),tostring(TargetResources.userPrincipalName)
 | extend DisplayName = tostring(UsersInsights.AccountDisplayName),
 UserPrincipalName = iff(UserPrincipalName has "#EXT#",replace("_","@",tostring(split(UserPrincipalName, "#")[0])),UserPrincipalName),
 UserName = iff(UserName has "#EXT#",replace("_","@",tostring(split(UserPrincipalName, "#")[0])),UserName)
 | sort by TimeGenerated desc	
 | project TimeGenerated, UserName, UserPrincipalName, UsersInsights, ActivityType, ActionType, ["TargetUser"]=Target,ActivityInsights ,SourceIPAddress, SourceIPLocation, SourceDevice, DevicesInsights
