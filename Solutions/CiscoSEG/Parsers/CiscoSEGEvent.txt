// Usage Instructions: 
// Paste below query in log analytics, click on Save button and select as Function from drop down by specifying function name as CiscoSEGEvent and specifying Legacy Category.
// This function maps Cisco SEG Consolidated Event Logs to normalization schema (https://docs.microsoft.com/azure/sentinel/normalization-schema).
// Function usually takes 10-15 minutes to activate. You can then use function alias from any other queries (e.g. CiscoSEGEvent | take 10).
// Reference : Using functions in Azure monitor log queries : https://docs.microsoft.com/azure/azure-monitor/log-query/functions
CommonSecurityLog
| where DeviceEventClassID =~ 'ESA_CONSOLIDATED_LOG_EVENT'
| extend EventStartTime = todatetime(extract(@"startTime=(\w{3}\s\w{3}\s\d{1,2}\s\d{2}:\d{2}:\d{2}\s\d{4})", 1, AdditionalExtensions))
| extend EventEndTime = todatetime(extract(@"endTime=(\w{3}\s\w{3}\s\d{1,2}\s\d{2}:\d{2}:\d{2}\s\d{4})", 1, AdditionalExtensions))
| extend NetworkDirection = case(CommunicationDirection == '0', 'Incoming', 'Outgoing')
| extend EventType = Activity
| extend FileName = extract(@"ESAAttachmentDetails=\{\'(.*?)\'", 1, AdditionalExtensions)
| extend FileHashSha256 = extract(@"\'fileHash\':\s?\'(.*?)\'", 1, AdditionalExtensions)
| extend FileSize = toint(extract(@"\'fsize\':\s?(\d+)", 1, AdditionalExtensions))
| extend EventStatus = extract(@"ESADaneStatus=(success|failure)", 1, AdditionalExtensions)
| extend DvcHostname = extract(@"ESADaneHost=(\S+)", 1, AdditionalExtensions)
| extend AdditionalFields = extract_all(@"(?P<key>[a-zA-Z0-9- ]+)=(?P<value>[a-zA-Z0-9-_:/@.#{}'' ]+)", dynamic(["key","value"]), tostring(AdditionalExtensions))
| mv-apply AdditionalFields on (
    summarize AdditionalFields = make_list(pack(tostring(AdditionalFields[0]), AdditionalFields[1]))
    )
| project-rename EventVendor = DeviceVendor
               , EventProduct = DeviceProduct
               , EventId = DeviceEventClassID
               , EventSeverity = LogSeverity
               , DvcAction = DeviceAction
               , DvcIpAddr = DeviceAddress
               , EventMessage = Message
               , EventProductVersion = DeviceVersion
               , SerialNumber = DeviceExternalID
               , DvcInboundInterface = DeviceInboundInterface
               , DvcOutboundInterface = DeviceOutboundInterface
               , DstUserName = DestinationUserName
               , SrcUserName = SourceUserName
               , MailPolicy = DeviceCustomString1
               , SrcGeoCountry = DeviceCustomString2
               , ThreatCategory = DeviceCustomString3
               , EventOriginalUid = DeviceCustomString4
               , MailLanguage = DeviceCustomString5
               , SdrRepScore = DeviceCustomString6
               , SbrsScore = DeviceCustomFloatingPoint1
| project-away Activity
             , DeviceCustomString1Label
             , DeviceCustomString2Label
             , DeviceCustomString3Label
             , DeviceCustomString4Label
             , DeviceCustomString5Label
             , DeviceCustomString6Label
             , DeviceCustomFloatingPoint1Label
             , AdditionalExtensions
