// Usage Instruction : 
// Paste below query in log analytics, click on Save button and select as Function from drop down by specifying function name and alias (e.g. afad_parser).
// Reference : Using functions in Azure monitor log queries : https://docs.microsoft.com/azure/azure-monitor/log-query/functions

let CodenameTable=datatable(Codename: string, Explanation: string) [
"test-checker-codename", "This is a test checker",
"", "Not an alert",
"C-ADM-ACC-USAGE", "Recent use of the default administrator account",
"C-UNCONST-DELEG", "Dangerous delegation",
"C-PASSWORD-DONT-EXPIRE", "Accounts with never expiring passwords",
"C-USERS-CAN-JOIN-COMPUTERS", "Users allowed to join computers to the domain",
"C-CLEARTEXT-PASSWORD", "Potential clear-text password",
"C-PROTECTED-USERS-GROUP-UNUSED", "Protected Users group not used",
"C-PASSWORD-POLICY", "Weak password policies are applied on users",
"C-GPO-HARDENING", "Domain without computer-hardening GPOs"
];
let Common = AlsidForADLog_CL
| parse RawData with
                     Time:datetime  " "
                     Host:string  " "
                     Product:string "["
                     PID:int "]: \""
                     MessageType:int "\" \""
                     AlertID:int "\" \""
                     Forest:string "\" \""
                     Domain:string "\" "
                     DistinctPart:string;
let Deviances = Common
| where MessageType == 0 | parse DistinctPart with "\""
                     Codename:string "\" \""
                     Severity:string "\" \""
                     ADObject:string "\" \""
                     DevianceID:string "\" \""
                     ProfileID:string "\" \""
                     ReasonCodename:string "\" \""
                     EventID:string "\""
                     Attributes:string "\r\n";
let Changes = Common
| where MessageType == 1
| parse kind=regex DistinctPart with "\""
                     ADObject:string "\" \""
                     EventID:string "\" \""
                     EventType:string "\" "
                     Attributes:string "\r?\n";
union Changes, Deviances
| project-away DistinctPart, Product, _ResourceId, _SubscriptionId
| lookup kind=leftouter CodenameTable on Codename;