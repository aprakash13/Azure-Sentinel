// Title:           Juniper SRX Parser
// Author:          Microsoft
// Version:         1.0
// Last Updated:    11/17/2020
// Comment:         Inital Release
//  
// DESCRIPTION:
// This parser takes raw Juniper SRX logs from a Syslog stream and parses the logs into a normalized schema.
//
// USAGE:
// 1. Open Log Analytics/Azure Sentinel Logs blade. Copy the query below and paste into the Logs query window. 
// 2. In the query window, on the second line of the query, enter the hostname(s) of your Juniper SRX device(s) and any other unique identifiers for the logstream. 
//    For example: | where Computer in ("server1", "server2")
// 3. Click the Save button above the query. A pane will appear on the right, select "as Function" from the drop down. Enter a Function Name.
//    It is recommended to name the Function Alias, as JuniperSRX
// 4. Kusto Functions can typically take up to 15 minutes to activate. You can then use Function Alias for other queries.
//
// REFERENCES: 
// Using functions in Azure monitor log queries: https://docs.microsoft.com/azure/azure-monitor/log-query/functions
// 
// LOG SAMPLES:
// This parser assumes the raw log are formatted as follows:
//
// <14>Nov 14 06:13:55 JuniperSRX_FW4 RT_FLOW session created 172.20.22.237/161->134.207.87.33/162 0x0 None 155.117.69.171/52344->134.207.87.33/162 0x0 source rule source-nat-rule N/A N/A 17 trust_to_Untrust_Internet trust untrust 25620 N/A(N/A) reth4.0 UNKNOWN UNKNOWN UNKNOWN
// <37>Nov 14 06:14:33 JuniperSRX_FW1 sshd Login failed for user 'root' from host '123.45.67.89'
// <38>Nov 14 06:13:47 JuniperSRX_FW2 sshd[42227] Disconnected from 183.13.25.46 [preauth]
// <14>Nov 14 06:21:12 JuniperSRX_FW1 RT_FLOW session closed TCP FIN: 172.20.21.51/49266->172.20.22.254/80 0x0 junos-http 172.20.21.51/49266->172.20.22.254/80 0x0 N/A N/A N/A N
//
let LogHeader = Syslog
| where Computer in ("server1", "server2") // server1 and server2 are examples, replace this list with your Juniper SRX devices
| extend Parser = extract_all(@"\<\d+\>(\w+\s\d+\s[0-9\:]+)\s(\w+)\s?(\w+)?\[?([\w]+)?\]?\s([\S\s]+)", dynamic([1,2,3,4,5]),SyslogMessage)
| mv-expand Parser
| extend EventTimestamp = tostring(Parser[0]),
         DvcHostname = tostring(Parser[1]),
         EventType = tostring(Parser[2]),
         ProcessId = toint(Parser[3]),
         Message = trim("- ",tostring(Parser[4]))
| project-away Parser;
let SshEvents = LogHeader
| where EventType =~ "sshd"
| extend Parser = extract_all(@"password\sfor\s(\w+)\sfrom\s([0-9.]+)\sport\s(\d+)",dynamic([1,2,3]), Message)
| mv-expand Parser
| extend UserName = tostring(Parser[0]),
         SrcIpAddr = tostring(Parser[1]),
         SrcPortNumber = toint(Parser[2])
| extend EventName = extract(@"^(\w+\s?\w+?)\s(for|from)",1, Message)
| extend UserName = iif(isempty(UserName), extract(@"for\suser\s\'(\w+)\'\sfrom\shost\s\'([0-9\.]+)\'",1, Message), UserName)
| extend UserName = iif(isempty(UserName), extract(@"PAM_USER\:\s(\w+)",1, Message), UserName)
| extend UserName = iif(isempty(UserName), extract(@"user:\s(\w+)",1, Message), UserName)
| extend SrcIpAddr = iif(isempty(SrcIpAddr), extract(@"from\s(host)?\s?\'?([0-9.]+)\'?",2, Message), SrcIpAddr)
| extend SrcIpAddr = iif(isempty(SrcIpAddr), extract(@"source\:\s([0-9.]+)\,",1, Message), SrcIpAddr)
| project-away Parser;
let IdsEvents = LogHeader
| where EventType == "RT_IDS"
| extend SrcIpAddr = extract(@"source\:\s([0-9.]+)\,?\:?(\d+)?",1, Message),
         SrcPortNumber = toint(extract(@"source\:\s([0-9.]+)\,?\:?(\d+)?",2, Message)),
         DstIpAddr = extract(@"destination\:\s([0-9.]+)\,?",1, Message),
         DstPortNumber = toint(extract(@"destination\:\s([0-9.]+)\,?\:?(\d+)?",2, Message)),
         ProtocolId = toint(extract(@"protocol-id\:\s([0-9.]+)\,",1, Message)),
         ZoneName = extract(@"zone\sname\:\s([\w]+)\,",1, Message),
         InterfaceName = extract(@"interface\sname\:\s([\w\.]+)\,",1, Message),
         Action = extract(@"action\:\s([\w\-\.]+)",1, Message);
let FlowEvents = LogHeader
| where EventType == "RT_FLOW"
| extend Parser = extract_all(@"^([\w\s\-]+)(\s\d|\:)\s?([\d\.]+)\/(\d+)\-\>([\d\.]+)\/(\d+)\s(\w+)?\s?([\w\-]+)\s([\d\.]+)\/(\d+)\-\>([\d\.]+)\/(\d+)\s([\S\s]+)",dynamic([1,2,3,4,5,6,7,8,9,10,11,12,13]), Message)
| mv-expand Parser
| extend EventName = tostring(Parser[0]),
         SrcIpAddr = tostring(Parser[2]),
         SrcPortNumber = toint(Parser[3]),
         DstIpAddr = tostring(Parser[4]),
         DstPortNumber = toint(Parser[5]),
         ServiceName = tostring(Parser[7]),
         SrcNatIpAddr = tostring(Parser[8]),
         SrcNatPortNumber = toint(Parser[9]),
         DstNatIpAddr = tostring(Parser[10]),
         DstNatPortNumber = toint(Parser[11]),
         Substring = tostring(Parser[12])
| extend Parser2 = extract_all(@"(0x0/s)?([\S]+)\s([\S]+)\s([\S]+)\s([\S]+)\s(\d+)\s([\S]+)\s([\S]+)\s([\S]+)\s(\d+)",dynamic([1,2,3,4,5,6,7,8,9,10]), Substring)
| mvexpand Parser2
| extend ProtocolId = toint(Parser2[5]),
         PolicyName = tostring(Parser2[6]),
         SrcNatRuleName = tostring(Parser2[7]),
         DstNatRuleName = tostring(Parser2[8]),
         SessionId = toint(Parser2[9])
| project-away Parser, Parser2, Substring;
let AllOtherEvents = LogHeader
| where EventType !in ("sshd","RT_IDS","RT_FLOW")
| extend EventName = extract(@"^([\w\s]+)\s(0)",1, Message);
union SshEvents, IdsEvents, FlowEvents, AllOtherEvents
| extend EventName = iif(isempty(EventName), extract(@"^([\w\s]+)\s(\d.*)",1, Message), EventName)
