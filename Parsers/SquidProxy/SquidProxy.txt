// Title:           Squid Proxy Parser
// Author:          Microsoft
// Version:         1.0
// Last Updated:    10/26/2020
// Comment:         Inital Release
//  
// DESCRIPTION:
// This parser takes raw Squid Proxy logs from a Custom Log stream and parses the data into a normalized schema
//
// USAGE:
// 1. Open Log Analytics/Azure Sentinel Logs blade. Copy the query below and paste into the Logs query window. 
// 2. In the query window, on the second line of the query, enter the hostname(s) of your Squid Proxy device(s) and any other unique identifiers for the logstream. 
//    For example: | where Computer in ("server1, server2")
// 3. Click the Save button above the query. A pane will appear on the right, select "as Function" from the drop down. Enter a Function Name.
//    It is recommended to name the Function Alias, as SquidProxy
// 4. Kusto Functions usually take 10-15 minutes to activate. You can then use Function Alias for other queries
//
// REFERENCES: 
// Using functions in Azure monitor log queries: https://docs.microsoft.com/azure/azure-monitor/log-query/functions
// 
// LOG SAMPLES:
// This parser assumes the raw log are formatted as follows:
//
// cache.log:
// 2020/04/11 00:28:57 kid1| Set Current Directory to /var/cache/squid
//
// access.log:
// 1603399381.121    346 ::1 TCP_MISS/301 419 GET http://httpvshttps.com/ - HIER_DIRECT/11.44.22.33 text/html
// 
// 
SquidProxy_CL
| extend AccessRawLog = extract_all(@"^(\d+\.\d+)\s+(\d+)\s(\S+)\s([A-Z_]+)\/(\d+)\s(\d+)\s([A-Z]+)\s(\S+)\s(\S+)\s([A-Z_]+)\/(\S+)\s(\S+)",dynamic([1,2,3,4,5,6,7,8,9,10,11,12]),RawData)
| mv-expand todynamic(AccessRawLog)
| extend EpochTime = tostring(AccessRawLog[0]),
         Duration = toint(AccessRawLog[1]),
         SrcIpAddr = tostring(AccessRawLog[2]),
         ResultCode = tostring(AccessRawLog[3]),
         StatusCode = toint(AccessRawLog[4]),
         Bytes = toint(AccessRawLog[5]),
         HttpRequestMethod = tostring(AccessRawLog[6]),
         Url = tostring(AccessRawLog[7]),
         Username = tostring(AccessRawLog[8]),
         PeerStatus = tostring(AccessRawLog[9]),
         PeerHost = tostring(AccessRawLog[10]),
         ContentType = tostring(AccessRawLog[11])
| extend EventEpochTime = tolong(replace(@"\.","",EpochTime))
| extend EventTime = datetime(1970-01-01) + tolong(EventEpochTime/1000) * 1sec
| extend CacheRawLog = extract_all(@"^([0-9\/]+\s[0-9\:]+)\s(\S+)\s([\S\s]+)", dynamic([1,2,3]), RawData)
| mv-expand CacheRawLog
| extend CacheEventTime = todatetime(replace("/","-",tostring(CacheRawLog[0]))),
         Description = tostring(CacheRawLog[2])
| extend EventTime = iif(isempty(EventTime), CacheEventTime, EventTime)
| project-away AccessRawLog, RawData, EpochTime, EventEpochTime, CacheRawLog, CacheEventTime