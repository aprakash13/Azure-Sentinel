// This is a Query Parser that is used to map Syslog messsages sent from a CyberArk Digital Vault for creating Dashboards and Alerts.
//
// This is an overall query that captures ALL of the Audit Codes that are sent from the Vault.
// This has no filters to catch any specific messages and will return all messages sent from the Vault
//
// Usage Instruction : 
// Paste the query below into log analytics, click Save button and select as Function from drop down by specifying function name and alias.
// Ensure that your alias is set to "CyberArk_Syslog"
// Function usually takes 10-15 minutes to activate. You can then use function alias from any other queries 
// (example: CyberArk_Syslog | take 10).
// Reference : Using functions in Azure monitor log queries : https://docs.microsoft.com/azure/azure-monitor/log-query/function

Syslog
| where ProcessName contains "Cyber-Ark"
| extend Time = EventTime 
| extend Source = Computer 
| extend MessageID = ProcessID  
| extend Vendor = ProcessName 
| extend deviceAction = extract("act=(.*?)suser=", 1, SyslogMessage) 
| extend sourceUserName = extract("suser=(.*?)fname=", 1, SyslogMessage) 
| extend fileName = extract("fname=(.*?)dvc=", 1, SyslogMessage) 
| extend deviceAddress = extract("dvc=(.*?)shost=", 1, SyslogMessage) 
| extend sourceHostName = extract("shost=(.*?)dhost=", 1, SyslogMessage) 
| extend destinationHostName = extract("dhost=(.*?)duser=", 1, SyslogMessage) 
| extend destinationUserName = extract("duser=(.*?)externalId=", 1, SyslogMessage) 
| extend externalId = extract("externalId=(.*?)app=", 1, SyslogMessage) 
| extend applicationProtocol = extract("app=(.*?)reason=", 1, SyslogMessage) 
| extend Reason = extract("reason=(.*?)cs1Label=", 1, SyslogMessage) 
| extend affectedUserName = extract("cs1=(.*?)cs2Label=", 1, SyslogMessage) 
| extend safeName = extract("cs2=(.*?)cs3Label=", 1, SyslogMessage) 
| extend deviceType = extract("cs3=(.*?)cs4Label=", 1, SyslogMessage) 
| extend Database = extract("cs4=(.*?)cs5Label=", 1, SyslogMessage) 
| extend otherInfo = extract("cs5=(.*?)cn1Label=", 1, SyslogMessage) 
| extend requestID = extract("cn1=(.*?)cn2Label=", 1, SyslogMessage) 
| extend ticketID = extract("cn2=(.*$)", 1, SyslogMessage) 
| extend Message = extract("msg=(.*$)", 1, SyslogMessage) 
