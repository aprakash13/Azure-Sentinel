// Name: Messages with the provided domain
// Description: Messages with the provided domain in range of +-10m
//
// Id: 6681a9cc-40fb-4b09-8fe7-36cc339e65ee
//
// Entity: Domain
// Input: Domain
// Output: Account, IPAddress
//
// QueryPeriod: +-10m, change as needed
//
// Data Source: CommonSecurityLog
//
// Tactics: #Discovery, #Lateral Movement, #Collection
//
let GetCSLogEventsByDomain = (suspiciousEventTime:datetime, v_Domain:string){
let v_StartTime = suspiciousEventTime-1h;
let v_EndTime = suspiciousEventTime+1h;
CommonSecurityLog
| where TimeGenerated between (v_StartTime .. v_EndTime)
| where Message has v_Domain
| summarize min(TimeGenerated), max(TimeGenerated) by DeviceVendor, DeviceProduct, DeviceEventClassID, Computer, Message
| project min_TimeGenerated, max_TimeGenerated, DeviceVendor, DeviceProduct, DeviceEventClassID, Computer, Message
};
// change datetime value and <processname> value below
GetCSLogEventsByDomain(datetime('2019-01-30T10:36:07Z'), "<domain>")