// Name: Alerts related to IP
// Description: Any Alerts that fired related to a given IpAddress during the range of +6h and -3d
//
// Entity: IpAddress
// Input: IpAddress, ClientIP, Dns Answer
// Output: Host, Account
//
// QueryPeriod: +6h and -3d default, change as needed
//
// Data Source: SecurityAlert
//
// Tactics: #Persistence, #Discovery, #Lateral Movement, #Collection
//
let GetAllAlertsWithIp = (suspiciousEventTime:datetime, v_ipAddress:string){
//-3d and +6h as some alerts fire after accumulation of events
let v_StartTime = suspiciousEventTime-3d;
let v_EndTime = suspiciousEventTime+6h;
SecurityAlert
| where TimeGenerated between (v_StartTime .. v_EndTime)
// expand JSON properties
| extend Extprop = parsejson(ExtendedProperties)
| extend Computer = toupper(tostring(Extprop["Compromised Host"]))
| extend Account =  tolower(tostring(Extprop["User Name"]))
| where ExtendedProperties contains v_ipAddress or Entities contains v_ipAddress
| project StartTimeUtc = StartTime, EndTimeUtc = EndTime, AlertName, Computer, Account, ExtendedProperties, Entities 
};
// change datetime value and <ipaddress> value below
GetAllAlertsWithIp(datetime('2019-02-05T10:02:51.000'), ("<ipaddress>"))