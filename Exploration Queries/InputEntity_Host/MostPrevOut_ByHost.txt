// Name: WireData Most Prevalent Outbound Connections by Host
// Description: Top 10 (most prevalent) for WireData Outbound event by a given Host in the range of +-30m
//
// Id: 9a0c01e4-3330-4050-88b1-28a4dff820cd
//
// Entity: Host
// Input: Host, HostName
// Output: Computer, Process, IP
//
// QueryPeriod: +-30m
//
// Data Source: WireData
//
// Tactics: #Probing, #Discovery, #LateralMovement, #Collection
//
let GetWireDataOutboundWithHost = (suspiciousEventTime:datetime, v_Host:string){
let v_StartTime = suspiciousEventTime-1d;
let v_EndTime = suspiciousEventTime+1d;
WireData
| where TimeGenerated between (v_StartTime .. v_EndTime)
| where Direction == "Outbound" 
| where Computer has v_Host
| summarize min(SessionStartTime), count() by Computer, ProcessName , LocalIP, LocalPortNumber, RemoteIP, Direction, ApplicationProtocol
| top 10 by count_ desc
| project min_SessionStartTime, Computer, ProcessName , LocalIP, LocalPortNumber, RemoteIP, Direction, ApplicationProtocol, count_
};
// change datetime value and <processname> value below
GetWireDataOutboundWithHost(datetime('2019-03-31T10:36:07Z'), "<hostname>")